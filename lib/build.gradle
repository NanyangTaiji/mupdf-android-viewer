apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'com.artifex.mupdf'
version = '1.21.0a'

dependencies {
	implementation 'androidx.appcompat:appcompat:1.3.1'
	implementation 'com.google.android.material:material:1.6.1'
    if (file('../jni/build.gradle').isFile())
		api project(':jni')
	else
		api 'com.artifex.mupdf:fitz:1.21.0'
}

android {
	compileSdkVersion 31
	defaultConfig {
		minSdkVersion 21
		targetSdkVersion 31
	}
	publishing {
		singleVariant("release") {
			withSourcesJar()
		}
	}
}

project.afterEvaluate {
	publishing {
		publications {
			release(MavenPublication) {
				from components.release
				pom {
					groupId = 'com.artifex.mupdf.viewer'
					artifactId = 'mupdf'
					licenses {
						license {
							name = 'GNU Affero General Public License'
							url = 'https://www.gnu.org/licenses/agpl-3.0.html'
						}
					}
					scm {
						connection = 'scm:git@github.com:TeamAmaze/mupdf-android-viewer.git'
						developerConnection = 'scm:git@github.com:TeamAmaze/mupdf-android-viewer.git'
						url = 'https://teamamaze.xyz/'
					}
					description = 'Mupdf Android'
					version = '1.0.0'
				}
			}
		}
		repositories {
			maven {
				name 'Local'
				if (project.hasProperty('MAVEN_REPO')) {
					url = MAVEN_REPO
				} else {
					url = "file://${System.properties['user.home']}/MAVEN"
				}
			}
			mavenLocal()
			maven { url "https://jitpack.io" }
		}
	}
}
